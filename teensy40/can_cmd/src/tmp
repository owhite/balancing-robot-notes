


#include <Arduino.h>
#include <FlexCAN_T4.h>

// Teensy 4.0 CAN1: TX=22, RX=23
FlexCAN_T4<CAN1, RX_SIZE_256, TX_SIZE_16> Can1;

void canSniff(const CAN_message_t &msg) {
  Serial.printf("RX %s ID=0x%08lX DLC=%u : ",
                msg.flags.extended ? "EXT" : "STD",
                msg.id & 0x1FFFFFFF,
                msg.len);
  for (int i = 0; i < msg.len; i++) {
    Serial.printf("%02X ", msg.buf[i]);
  }
  Serial.println();
}

void setup() {
  Serial.begin(115200);
  while (!Serial && millis() < 2000) {}
  Serial.println("CAN1 listen-only sniffer @500k, pins 22/23");

  Can1.begin();
  Can1.setBaudRate(500000);          // ESC is known good at 500k
  Can1.setMaxMB(16);
  Can1.enableFIFO();
  Can1.enableFIFOInterrupt();
  Can1.onReceive(canSniff);
  Can1.enableMBInterrupts();

  // Force silent mode: listen only, no ACKs
  FLEXCAN1_CTRL1 |= FLEXCAN_CTRL_LOM;
}

void loop() {
  Can1.events();   // keep CAN hardware serviced
}
